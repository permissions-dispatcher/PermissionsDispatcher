apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = GROUP_ID
version = VERSION

targetCompatibility = JavaVersion.VERSION_1_6
sourceCompatibility = JavaVersion.VERSION_1_6

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId GROUP_ID
            artifactId ARTIFACT_ID_ANNOTATION
            version VERSION
        }
    }
}

bintray {
    publications = ['MyPublication']
    user = bintrayUser
    key = bintrayKey
    dryRun = dryRun
    publish = true

    pkg {
        repo = REPO
        userOrg = USER
        name = ARTIFACT_ID_ANNOTATION
        vcsUrl = VCS_URL
        licenses = LICENSES
        version {
            name = VERSION
            desc = DESCRIPTION
        }
    }
}

// Non-Android project: Artifactory Plugin adds only 1 publication
project.ext.publicationName = "maven"
apply from: rootProject.file('gradle/gradle-artifactory-upload.gradle')
