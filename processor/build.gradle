import org.gradle.internal.jvm.Jvm
import permissions.dispatcher.AndroidJarDependencyPlugin
import permissions.dispatcher.AarToJarConversionPlugin

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: AndroidJarDependencyPlugin
apply plugin: AarToJarConversionPlugin

// Connect sources & tests to library project
convertAarsToJar.dependsOn ":library:assembleRelease"

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    compile "androidx.annotation:annotation:$ANDROIDX_LIBRARY_VERSION"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    compile "org.jetbrains.kotlinx:kotlinx-metadata-jvm:$KOTLIN_METADATA_VERSION"
    compile "com.squareup:javapoet:$JAVAPOET_VERSION"
    compile "com.squareup:kotlinpoet:$KOTLINPOET_VERSION"
    compile project(path: ':annotation')

    testCompileAar "androidx.legacy:legacy-support-v4:$ANDROIDX_LIBRARY_VERSION"
    testCompileAar project(path: ":library", configuration: "default")

    testCompile androidJar()
    testCompile files(Jvm.current().getToolsJar())
    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    testCompile "com.google.testing.compile:compile-testing:$COMPILE_TESTING_VERSION"
    testCompile "commons-io:commons-io:$COMMONS_IO_VERSION"
    testCompile "com.squareup.okio:okio:2.1.0"
    testCompile "androidx.legacy:legacy-support-v4:$ANDROIDX_LIBRARY_VERSION"
    testCompile "org.permissionsdispatcher:kompile-testing:$KOMPILE_TESTING_VERSION"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId GROUP_ID
            artifactId ARTIFACT_ID_PROCESSOR
            version VERSION
        }
    }
}

bintray {
    publications = ['MyPublication']
    user = bintrayUser
    key = bintrayKey
    dryRun = dryRun
    publish = true

    pkg {
        repo = REPO
        userOrg = USER
        name = ARTIFACT_ID_PROCESSOR
        vcsUrl = VCS_URL
        licenses = LICENSES
        version {
            name = VERSION
            desc = DESCRIPTION
        }
    }
}

// Non-Android project: Artifactory Plugin adds only 1 publication
project.ext.publicationName = "maven"
apply from: rootProject.file('gradle/gradle-artifactory-upload.gradle')
