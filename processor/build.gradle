import org.gradle.internal.jvm.Jvm
import permissions.dispatcher.AndroidJarDependencyPlugin
import permissions.dispatcher.AarToJarConversionPlugin

apply plugin: 'kotlin'
apply plugin: 'com.novoda.bintray-release'
apply plugin: AndroidJarDependencyPlugin
apply plugin: AarToJarConversionPlugin

// Connect sources & tests to library project
sourceSets.main.java.srcDir project(":library").file("src/main/java")
convertAarsToJar.dependsOn ":library:assembleRelease"

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    compile "com.squareup:javapoet:$JAVAPOET_VERSION"
    compile "com.squareup:kotlinpoet:$KOTLINPOET_VERSION"

    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "com.google.testing.compile:compile-testing:$COMPILE_TESTING_VERSION"
    testCompile "commons-io:commons-io:$COMMONS_IO_VERSION"
    testCompile androidJar()
    testCompile files(Jvm.current().getToolsJar())
    testCompileAar "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    testCompileAar "com.android.support:support-v13:$SUPPORT_LIBRARY_VERSION"
    testCompileAar project(path: ":library", configuration: "default")
}

publish {
    userOrg = USER
    groupId = GROUP_ID
    artifactId = ARTIFACT_ID_PROCESSOR
    version = VERSION
    desc = DESCRIPTION
    website = WEBSITE
    licences = LICENCES
}

// Non-Android project: Artifactory Plugin adds only 1 publication
project.ext.publicationName = "maven"
apply from: rootProject.file('gradle/gradle-artifactory-upload.gradle')
