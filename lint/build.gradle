apply plugin: 'java'

targetCompatibility = JavaVersion.VERSION_1_6
sourceCompatibility = JavaVersion.VERSION_1_6

configurations {
    lintChecks
}

dependencies {
    compile "com.android.tools.lint:lint-api:$LINT_VERSION"
    compile "com.android.tools.lint:lint-checks:$LINT_VERSION"
    compile "com.android.tools.lint:lint:$LINT_VERSION"

    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "com.android.tools.lint:lint-tests:$LINT_VERSION"
    testCompile "com.android.tools:testutils:$LINT_VERSION"

    lintChecks files(jar)
}

jar {
    manifest {
        attributes("Lint-Registry": "permissions.dispatcher.PermissionsDispatcherIssueRegistry")
    }
}

test {
    // Ensure that ANDROID_HOME is set when launching Lint tests
    // (non-standard shells won't fill the reference, leading to
    // "This test requires an Android SDK" errors at runtime)
    if (System.getenv("ANDROID_HOME") == null) {
        Properties localProps = new Properties()
        rootProject.file("local.properties").withReader { localProps.load(it) }
        environment "ANDROID_HOME", localProps.getProperty("sdk.dir")
    }
}
